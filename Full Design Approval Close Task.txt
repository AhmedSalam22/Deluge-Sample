/* 
	Change the status of Full Design Approval to Pending Approval in zoho project
*/ 

tiket = zoho.desk.getRecordById(869305456,"tickets",TicketID,"desk");
// get related project for the ticket
projectID = tiket.get("cf").get("cf_project_id");
//get portal for zoho project;
ck = zoho.projects.getPortals("project");
portal_id = ck.getJson("portals").getJson("id");
// search for Full Design Approval Task in zoho project
m = Map();
tn = "Full Design Approval";
m.put("search_term",tn);
m.put("module","tasks");
t = invokeurl
[
	url :"https://projectsapi.zoho.com/restapi/portal/" + portal_id + "/projects/" + projectID + "/search"
	type :GET
	parameters:m
	connection:"project"
];
info t;
search_result = t.get("tasks");
search_count = t.get("tasks_count").toNumber();
// if there is a result loop through all result to fined the specific one
if(search_count >= 1)
{
	for each  task in search_result
	{
		if(task.get("name") == tn)
		{
			taskID = task.get("id");
			break;
		}
	}
}
// revoke the blueprint for specific task in zoho project
response = invokeurl
[
	url :"https://projects.zoho.com/portal/restapi/portal/" + portal_id + "/projects/" + projectID + "/tasks/" + taskID + "/blueprint/removetaskblueprint?taskId=" + taskID
	type :POST
	connection:"project"
];
// change task status in zoho project into pending approval 
values_map = Map();
values_map.put("custom_status","2385580000000068235");
response = zoho.projects.update("ebi0rg",projectID,"tasks",taskID,values_map,"project");
// get the taskdetail to apply blueprint
detail = invokeurl
[
	url :"https://projects.zoho.com/portal/restapi/portal/" + portal_id + "/tasks/" + taskID + "/taskdetails/"
	type :GET
	connection:"project"
];
info detail;
// apply blueprint for specific task in zoho project 
TASK_STATUS_ID = detail.get("STATUS_VALUE");
info "TASK_STATUS_ID >>" + TASK_STATUS_ID;
applyblueprintpremeters = Map();
applyblueprintpremeters.put("projectId",projectID);
applyblueprintpremeters.put("bluePrintId","2385580000000068229");
applyblueprintpremeters.put("considerCriteria","false");
applyblueprintpremeters.put("taskId",taskID);
applyblueprintpremeters.put("taskBlueprintStatusMappingArray","[{\"TASK_STATUS_ID\":" + TASK_STATUS_ID + ",\"BLUEPRINT_STATUS_ID\":" + TASK_STATUS_ID + "}]");
applyBlueprint = invokeurl
[
	url :"https://projects.zoho.com/restapi/portal/" + portal_id + "/projects/" + projectID + "/tasks/" + taskID + "/blueprint/applytaskblueprint"
	type :POST
	parameters:applyblueprintpremeters
	connection:"project"
];