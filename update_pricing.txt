/* 
	Update the item price where is new bill created
*/
billID = bill.get("bill_id");
apiUrl = organization.get("api_root_endpoint");
organizationID = organization.get("organization_id");
info billID;
bills = zoho.books.getRecordsByID("bills",organizationID,billID,"books").get("bill");
lineItems = bills.get("line_items");

// items to be updated
itemsList = List();
for each  item in lineItems
{
	itemID = item.get("item_id");
	bcyRate = item.get("bcy_rate");
	itemMap = Map();
	itemMap.put("purchase_rate",bcyRate.round(2));
	itemMap.put("rate",bcyRate.round(2));
	itemMap.put("item_id",itemID);
	itemsList.add(itemMap);
}
// if there is an items in bills
if(itemsList.size() > 0)
{
	json = Map();
	json.put("JSONString",itemsList);
	result = invokeurl
	[
		url :apiUrl + "/items?organization_id=" + organizationID
		type :PUT
		parameters:json
		connection:"zbooks"
	];
	info "Result: " + result;
}
else
{
	info "Cannot update items cost price.";
}
