void closeTaskMany(string TicketID, list tasks)
{
	/*
	 close more than one task in zoho project
	*/
	
// get project id from ticket
tiket = zoho.desk.getRecordById(869305456,"tickets",TicketID,"zohodesk");
projectID = tiket.get("cf").get("cf_project_id");
// get portal id
ck = zoho.projects.getPortals("project");
portal_id = ck.getJson("portals").getJson("id");
// loop through each task that we want to close
for each  task_name in tasks
{
	// search for this task in zoho project
	m = Map();
	m.put("search_term",task_name);
	m.put("module","tasks");
	t = invokeurl
	[
		url :"https://projectsapi.zoho.com/restapi/portal/" + portal_id + "/projects/" + projectID + "/search"
		type :GET
		parameters:m
		connection:"project"
	];
	//info t;
	search_result = t.get("tasks");
	search_count = t.get("tasks_count").toNumber();
	// if there find a result for task
	if(search_count > 0)
	{
		// loop through each result to see if it's the same name that we look for
		for each  task in search_result
		{

			if(task.get("name") == task_name)
			{
				taskID = task.get("id");
				// close the task
				values_map = Map();
				values_map.put("percent_complete","100");
				response = zoho.projects.update("ebi0rg",projectID,"tasks",taskID,values_map,"project");
				info response;
				break;
			}
		}
	}
}
}